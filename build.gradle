plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'org.jmailen.kotlinter' version "3.10.0"
    id 'com.github.gmazzo.buildconfig' version '3.0.3'
    id "com.github.ben-manes.versions" version '0.42.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.github.pambrose.srcref'
version = '1.0.9'

buildConfig {
    buildConfigField('String', 'NAME', "\"${project.name}\"")
    buildConfigField('String', 'VERSION', "\"${project.version}\"")
    buildConfigField('String', 'RELEASE_DATE', "\"06/13/2022\"")
}

def mainName = "com.pambrose.srcref.Main"

application {
    mainClassName = mainName
}

repositories {
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation "io.ktor:ktor-server:$ktor_version"
    implementation "io.ktor:ktor-server-cio:$ktor_version"
    implementation "io.ktor:ktor-server-html-builder:$ktor_version"

    implementation "io.ktor:ktor-client-core:$ktor_version"
    implementation "io.ktor:ktor-client-cio:$ktor_version"

    implementation "org.jetbrains.kotlin-wrappers:kotlin-css:$css_version"

    implementation "com.github.pambrose.common-utils:core-utils:$utils_version"
    implementation "com.github.pambrose.common-utils:ktor-server-utils:$utils_version"

    implementation "org.apache.commons:commons-text:$text_version"

    implementation "com.papertrailapp:logback-syslog4j:$papertrail_version"
    implementation "io.github.microutils:kotlin-logging:$logging_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"

    testImplementation "io.kotest:kotest-runner-junit5:$kotest_version"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    zip64 true
    archiveFileName = 'srcref.jar'
    manifest {
        attributes('Implementation-Title': 'srcref')
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ['-Xbackend-threads=8',
                             '-opt-in=kotlin.time.ExperimentalTime',
                             '-opt-in=kotlinx.coroutines.DelicateCoroutinesApi',
        ]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += ['-Xbackend-threads=8']
    }
}

kotlinter {
    ignoreFailures = false
    reporters = ['checkstyle', 'plain']
    experimentalRules = false
    disabledRules = ["no-wildcard-imports", "indent", "final-newline", "comment-spacing", "max-line-length", "no-multi-spaces", "wrapping"]
}